{:title "Tables"
 :template "docpage.html"
 :order 3}
---

The table is one of the most flexible data structures in Janet and
is modeled after the associative array or dictionary. Values are
put into a table with a key, and can be looked up later with the
same key. Tables are implemented with an underlying open hash table, so
they are quite fast and cache friendly.

Any Janet value except @code`nil` and @code`NaN` can be a key or a value in a Janet
table, and a single Janet table can have any mixture of
types as keys and values.

## Creating tables

The easiest way to create a table is via a table literal.

@codeblock[janet](```
(def my-tab @{
 :key1 1
 "hello" "world!"
 1 2
 '(1 0) @{:another :table}})
```)

Another way to create a table is via the @code[table] function. This has the advantage
that @code[table] is an ordinary function that can be passed around like any other. For
most cases, a table literal should be preferred.

@codeblock[janet](```
(def my-tab (table
             :key1 1
             "hello" "world!"
             1 2
             '(1 0) @{:another :table}))
```)

## Getting and setting values

Like other data structures in Janet, values in a table can be retrieved
with the @code[get] function, and new values in a table can be added via the
@code[put] function or the @code[set] special. Inserting a value of @code`nil` into a table
removes the key from the table. This means tables cannot contain @code`nil` values.

@codeblock[janet](```
(def t @{})

(get t :key) #-> nil

(put t :key "hello")
(get t :key) #-> "hello")

(set (t :cheese) :cake)
(get t :cheese) #-> :cake

# Tables can be called as functions
# that look up the argument
(t :cheese) #-> :cake
```)

## Prototypes

All tables can have a prototype, a parent table that is checked when a
key is not found in the first table. By default, tables have no prototype.
Prototypes are a flexible mechanism that, among other things, can be used to
implement inheritance in Janet. Read more in the @link[/docs/prototypes.html][documentation for prototypes]

## Useful functions for tables

The Janet core library has many useful functions for manipulating tables. Below
is a non-exhaustive list.

@ul{@li{@code[frequencies]}
    @li{@code[keys]}
    @li{@code[kvs]}
    @li{@code[length]}
    @li{@code[merge-into]}
    @li{@code[merge]}
    @li{@code[pairs]}
    @li{@code[post-walk]}
    @li{@code[pre-walk]}
    @li{@code[table/getproto]}
    @li{@code[table/setproto]}
    @li{@code[update]}
    @li{@code[values]}
    @li{@code[walk]}
    @li{@code[zipcoll]}}

See the @link[/api/table.html][Table API] documentation for table-specific functions.
